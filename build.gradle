/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.3/userguide/java_library_plugin.html
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:5.3.0'    // RPM & DEB support
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
        classpath 'net.sf.proguard:proguard-gradle:6.1.0'
    }
}
// Apply the java-library plugin to add support for Java Library
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'nebula.ospackage'


rootProject.group = 'org.jd.cli'
//version = '1.0.0'
//targetCompatibility = 1.8
// Common configuration //
rootProject.version='1.0.0'
rootProject.ext.set('jdCoreVersion', '1.1.3')
sourceCompatibility = 1.8
targetCompatibility = '1.8'

jar.baseName = rootProject.name

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        sourceCompatibility = targetCompatibility = '1.8'
        options.compilerArgs << '-Xlint:deprecation'
        options.compilerArgs << '-Xlint:unchecked'
        options.encoding = 'UTF-8'
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.jd:jd-core:1.1.3'
        //implementation 'org.jd:jd-core:1.1.3'

        compile 'args4j:args4j:2.33'
        //implementation 'args4j:args4j:2.33'

        testImplementation 'junit:junit:4.12'
        compile fileTree(dir: 'lib', include: '**/*.jar')
    }

    configurations {
        provided
        compile.extendsFrom provided
    }
}

// 'cleanIdea' task extension //
cleanIdea.doFirst {
    delete project.name + '.iws'
    delete 'out'
    followSymlinks = true
}

jar {
    dependsOn subprojects.tasks['jar']

    manifest {
        attributes 'Main-Class': 'org.jd.cli.App',
                'JD-GUI-Version': project.version,
                'JD-Core-Version': project.jdCoreVersion
    }

    from configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}

    exclude 'META-INF/licenses/**', 'META-INF/maven/**', 'META-INF/INDEX.LIST'
    exclude '**/ErrorStrip_*.properties', '**/RSyntaxTextArea_*.properties', '**/RTextArea_*.properties'
    exclude '**/FocusableTip_*.properties', '**/RSyntaxTextArea_License.txt'
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

// Minify JAR file //
task proguard(type: proguard.gradle.ProGuardTask, dependsOn: 'jar') {
    configuration 'src/proguard/resources/proguard.config.txt'
    injars jar.archivePath
    outjars 'build/libs/' + project.name + '-' + project.version + '-min.jar'
    libraryjars System.getProperty('java.home') + '/lib/rt.jar'
    libraryjars System.getProperty('java.home') + '/jmods/'
}

task copyWinmergePlugins(type: Copy, dependsOn: 'proguard') {
    from 'src/WinmergePlugins'
    from('build/libs/' + project.name + '-' + project.version + '-min.jar') {
        into 'WinMergePlugins/forJar'
    }
    into 'build/WinmergePlugins'
}

// Java executable wrapper for Windows //
launch4j {
    createExe.dependsOn 'proguard'

    version = textVersion = project.version
    fileDescription = productName = 'JD-CLI'
    errTitle 'JD-CLI Windows Wrapper'
    copyright 'JD-CLI (C) 2008-2019 Emmanuel Dupuy'
    icon projectDir.path + '/src/launch4j/resources/images/jd-gui.ico'
    jar projectDir.path + '/' + proguard.outJarFiles[0]
    bundledJrePath = '%JAVA_HOME%'

    headerType = 'console'
}

// Packages for Linux //
ospackage {
    buildDeb.dependsOn 'proguard'
    buildRpm.dependsOn 'proguard'

    license = file('LICENSE')
    maintainer 'syany <kz.github.syany@gmail.com>'
    os LINUX
    packageDescription 'JD-CLI, a standalone command line Java sources from CLASS files'
    packageGroup 'java'
    packageName project.name
    release '0'
    summary 'A Java Decompiler Cli'
    url 'https://github.com/java-decompiler/jd-gui'

    into '/opt/' + project.name
    from (proguard.outJarFiles[0]) {
        fileMode 0755
    }
    from ('src/linux/resources/') {
        fileMode 0755
    }
    from 'LICENSE', 'NOTICE', 'README.md'

    postInstall 'cd /opt/' + project.name + '; ln -s ./' + file(proguard.outJarFiles[0]).name + ' ./jd-cli.jar; xdg-icon-resource install --size 128 --novendor ./jd_icon_128.png jd-cli; xdg-desktop-menu install ./*.desktop'
    preUninstall 'cd /opt/' + project.name + '; rm -f ./jd-cli.jar; rm -fr ./ext; xdg-desktop-menu uninstall ./*.desktop'
}

// Distributions for OSX and Windows //
distributions {
    createExe.dependsOn 'copyWinmergePlugins'

    osx.contents {
        into('JD-CLI.app/Contents') {
            from('src/osx/resources') {
                include 'Info.plist'
                expand VERSION: project.version,
                       JAR: file(proguard.outJarFiles[0]).name
            }
        }
        into('JD-CLI.app/Contents/MacOS') {
            from('src/osx/resources') {
                include 'universalJavaApplicationStub.sh'
                fileMode 0755
            }
        }
        into('JD-CLI.app/Contents/Resources/Java') {
            from proguard.outJarFiles[0]
        }
        from 'LICENSE', 'NOTICE', 'README.md'
    }
    windows.contents {
        from 'build/launch4j/jd-cli.exe'
        from 'LICENSE', 'NOTICE', 'README.md'
        from 'build/WinmergePlugins'
    }

    installWindowsDist.dependsOn createExe
    windowsDistTar.dependsOn createExe
    windowsDistZip.dependsOn createExe

    installOsxDist.dependsOn 'proguard'
    osxDistTar.dependsOn 'proguard'
    osxDistZip.dependsOn 'proguard'
}

build.finalizedBy buildDeb
build.finalizedBy buildRpm
